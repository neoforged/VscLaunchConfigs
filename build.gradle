import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
}

repositories {
    mavenCentral()
}

gradleutils {
    setupSigning()
    setupCentralPublishing()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // api deps go into enduser classpath
    
    // implementation are local deps
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation 'org.junit.platform:junit-platform-console-standalone:1.10.0'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

group = 'net.neoforged'
version = gradleutils.version

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            gradleutils.sign(it)

            pom {
                name = project.name
                description = 'A Java library for generating Vsc Launch Configurations (.launch files).'

                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, License.LGPL_v2)
                pomUtils.githubRepo(it, 'VscLaunchConfigs')
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
        maven {
            name 'test'
            url file('repo')
        }
    }
}
